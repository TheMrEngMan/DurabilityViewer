buildscript {
    dependencies {
        classpath 'de.guntram.mcmod:crowdin-translate:1.4+1.19-pre2'
    }
    repositories {
	maven {
        name = 'CrowdinTranslate source'
		url = "https://minecraft.guntram.de/maven/"
	}
    }
}
plugins {
	id 'fabric-loom' version '0.12-+'
	id "com.modrinth.minotaur" version "2.+"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: 'de.guntram.mcmod.crowdin-translate'
crowdintranslate.crowdinProjectName = 'durabilityviewer'
crowdintranslate.verbose = false

repositories {
	maven {
		url = "https://maven.fabricmc.net/"
	}
	maven {
		url = "https://minecraft.guntram.de/maven/"
	}
    maven {
            url = "https://maven.terraformersmc.com/releases/"
    }
	maven {
		name = "Ladysnake Libs"
		url = "https://ladysnake.jfrog.io/artifactory/mods"
	}
	maven {
		url = "file://tmp/mymavenrepo/"
	}    
}

sourceCompatibility = 17
targetCompatibility = 17

ext.Versions = new Properties()
Versions.load(file("Versionfiles/mcversion-1.19.4.properties").newReader())

archivesBaseName = "durabilityviewer"
ext.projectVersion = "1.10.3"

version = "${Versions['minecraft_version']}-fabric${Versions['fabric_versiononly']}-${project.projectVersion}"

loom {
    mixin.defaultRefmapName = "DurabilityViewer-refmap.json";
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

dependencies {
    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
//     modImplementation "TechReborn:TechReborn-1.16:3.4.5+build.95"
    modImplementation "com.terraformersmc:modmenu:6.2.2"
    modImplementation "de.guntram.mcmod:crowdin-translate:${Versions['crowdintranslate_version']}"
    include    "de.guntram.mcmod:crowdin-translate:${Versions['crowdintranslate_version']}"
    
    modImplementation "de.guntram.mcmod:GBfabrictools:${Versions['gbfabrictools_version']}"
    include    "de.guntram.mcmod:GBfabrictools:${Versions['gbfabrictools_version']}"
    
	modImplementation "dev.emi:trinkets:3.6.0"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

processResources {
	dependsOn downloadTranslations
}

import com.modrinth.minotaur.TaskModrinthUpload

modrinth {
    projectId = 'durabilityviewer'
    versionName = project.archivesBaseName
    uploadFile = remapJar
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN") ?: "0"
    project {
        id = '262146'
        releaseType = 'release'
        addGameVersion("${Versions['minecraft_version']}")
        addGameVersion("Java "+targetCompatibility)
        addGameVersion("Fabric")
        mainArtifact(remapJar)
    }
    options {
        forgeGradleIntegration = false
    }
}
